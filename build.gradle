plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
}

group = 'br.com.actionlabs'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

ext{
    mapstructVersion = "1.6.3"
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-api:2.3.0'

    compileOnly 'org.projectlombok:lombok:1.18.30'
    compileOnly("org.mapstruct:mapstruct:${mapstructVersion}")

    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation("org.testcontainers:junit-jupiter")
    testImplementation("org.testcontainers:mongodb")
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom("org.testcontainers:testcontainers-bom:1.20.4")
    }
}

tasks.test {
    useJUnitPlatform()
    include '**/*Test.class'
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
}

tasks.register('integrationTest', Test) {
    useJUnitPlatform()
    description = 'Runs the integration tests.'
    group = 'verification'
    include '**/*IT.class'
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
    shouldRunAfter(tasks.test)
}
